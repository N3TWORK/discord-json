dependencies {
    compileOnly project(':api')
    compileOnly "$immutables_group:value:$immutables_version"
    annotationProcessor "$immutables_group:value:$immutables_version"
    api "$immutables_group:encode:$immutables_version"

    if (JavaVersion.current().isJava8()) {
        compileOnly "com.google.code.findbugs:annotations:3.0.1"
        testCompileOnly "com.google.code.findbugs:annotations:3.0.1"
    }
}

archivesBaseName = "discord-json-$project.name"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId archivesBaseName

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "$project.name"
                description = 'Discord entity domain as immutable Jackson objects'
                url = 'https://github.com/Discord4J/discord-json'
                organization {
                    name = 'Discord4J'
                    url = 'https://discord4j.com'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/Discord4J/discord-json/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0'
                        url = 'https://github.com/Discord4J/discord-json/LICENSE.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/N3TWORK/discord-json'
                    connection = 'scm:git:git://github.com/N3TWORK/discord-json.git'
                    developerConnection = 'scm:git:ssh://git@github.com:N3TWORK/discord-json.git'
                }
                developers {
                    developer {
                        name = 'The Discord4J Team'
                    }
                }
            }
        }
    }

    if (!isJitpack) {
        repositories {
            maven {
                if (isRelease) {
                    url 'https://nexus.n3twork.com/repository/maven-releases/'
                } else {
                    url 'https://nexus.n3twork.com/repository/maven-snapshots/'
                }
                def sonatypeUsername = findProperty('sonatypeUsername')
                def sonatypePassword = findProperty('sonatypePassword')
                if (sonatypeUsername != null && sonatypePassword != null) {
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName,
                'Implementation-Version': archiveVersion,
                'Automatic-Module-Name': 'discord4j.discordjson.encoding'
    }
}
